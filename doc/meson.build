
if get_option('docs')
    rst2html5 = find_program('rst2html5', 'rst2html5.py', 'rst2html5-docutils', required : false)

    if not rst2html5.found()
        error('rst2html5 from docutils is required to build documentation. Please install it, or disable the `docs` option if you don\'t want it.')
    endif

    rst_to_html = generator(rst2html5,
        arguments : [
            '--no-datestamp',
            '--no-source-link',
            '--smart-quotes=yes',
            '--no-compact-lists',
            '--stylesheet-path=minimal.css,plain.css,@0@/doc/taisei.css'.format(meson.source_root()),
            '@INPUT@',
            '@OUTPUT@',
        ],
        output : '@BASENAME@.html',
    )

    rst_docs = [
        'ENVIRON.rst',
        'GAME.rst',
    ]

    txt_docs = [
        'README.txt',
        'STORY.txt',
    ]

    eol = host_machine.system() == 'windows' ? 'crlf' : 'lf'

    foreach f : txt_docs
        custom_target(f,
            command : [eolconv, eol, '@INPUT@', '@OUTPUT@'],
            input : f,
            output : f,
            install : true,
            install_dir : doc_path
        )
    endforeach

    foreach f : rst_docs
        name = f.split('.')[0] + '.html'
        custom_target(name,
            command : [eolconv, eol, '@INPUT@', '@OUTPUT@'],
            input : rst_to_html.process(f),
            output : name,
            install : true,
            install_dir : doc_path,
            depend_files : files('taisei.css')
        )
    endforeach
endif
